extends PanelContainer

signal uncompressed()

var url: String = ""
var file_name: String = ""

var download_path: String = ""
var uncompress_task_id: int = -1

@onready var title_label: Label = $MarginContainer/VBoxContainer/TitleLabel
@onready var progress_bar: ProgressBar = $MarginContainer/VBoxContainer/ProgressBar
@onready var info_label: Label = $MarginContainer/VBoxContainer/InfoLabel
@onready var cancel_button: Button = $MarginContainer/VBoxContainer/CancelButton
@onready var http_request: HTTPRequest = $HTTPRequest
@onready var timer: Timer = $Timer

func _ready() -> void:
	tooltip_text = url
	title_label.text = file_name
	if DirAccess.make_dir_recursive_absolute(App.DOWNLOAD_DIR) != OK:
		return
	download_path = App.DOWNLOAD_DIR.path_join("%s.zip" % file_name)
	http_request.download_file = download_path
	if http_request.request(url) != OK:
		return
	cancel_button.disabled = false
	timer.start()

func _failed() -> void:
	info_label.text = "Failed"
	cancel_button.disabled = false
	timer.stop()
	if (download_path != ""
		and FileAccess.file_exists(download_path)):
		OS.move_to_trash(ProjectSettings.globalize_path(download_path))
	
func _on_timer_timeout() -> void:
	var total: int = http_request.get_body_size()
	if total <= 0:
		return
	var downloaded: int = http_request.get_downloaded_bytes()
	progress_bar.set_value_no_signal(float(downloaded) / float(total) * 99)
	
	
func _on_cancel_button_pressed() -> void:
	if http_request.get_http_client_status() == HTTPClient.STATUS_REQUESTING:
		http_request.cancel_request()
	queue_free()


func _on_http_request_request_completed(result: int, response_code: int, _headers: PackedStringArray, _body: PackedByteArray) -> void:
	if (result != HTTPRequest.RESULT_SUCCESS
		or response_code != 200):
		_failed()
		return
	timer.stop()
	progress_bar.set_value_no_signal(99)
	var finish_dir: String = App.ENGINE_DIR.path_join(file_name)
	if DirAccess.make_dir_recursive_absolute(finish_dir) != OK:
		_failed()
		return
	info_label.text = "Uncompressing..."
	cancel_button.disabled = true
	uncompress_task_id = WorkerThreadPool.add_task(_uncompress_file.bind(download_path, finish_dir))

func _uncompress_file(zip_path: String, output_dir: String) -> void:
	var zip: ZIPReader = ZIPReader.new()
	if zip.open(zip_path) != OK:
		zip.close()
		_failed.call_deferred()
		return
	var files: PackedStringArray = zip.get_files()
	for file_path: String in files:
		if file_path.ends_with("/"):
			if DirAccess.make_dir_recursive_absolute(output_dir.path_join(file_path)) != OK:
				zip.close()
				_failed.call_deferred()
				return
			continue
		var buffer: PackedByteArray = zip.read_file(file_path)
		if buffer.size() > 0:
			var full_path: String = output_dir.path_join(file_path)
			var file: FileAccess = FileAccess.open(full_path, FileAccess.WRITE)
			if file == null:
				zip.close()
				_failed.call_deferred()
				return
			file.store_buffer(buffer)
			file.close()
	zip.close()
	uncompressed.emit.call_deferred()

func _on_uncompressed() -> void:
	WorkerThreadPool.wait_for_task_completion(uncompress_task_id)
	info_label.text = "Completed"
	progress_bar.set_value_no_signal(100)
	cancel_button.text = "Close"
	cancel_button.disabled = false
	if (download_path != ""
		and FileAccess.file_exists(download_path)):
		OS.move_to_trash(ProjectSettings.globalize_path(download_path))
